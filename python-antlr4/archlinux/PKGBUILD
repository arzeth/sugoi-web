# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

_pkgname=antlr4
pkgname="python-${_pkgname}"
pkgver=4.9.3
pkgrel=2
pkgdesc="ANTLR4 runtime for Python"
arch=('any')
url="https://www.antlr.org/"
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm')
checkdepends=('python-tests')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/antlr/${_pkgname}/archive/${pkgver}.tar.gz"
        'fix-tests.patch')
sha256sums=('efe4057d75ab48145d4683100fec7f77d7f87fa258707330cadd1f8e6f7eecae'
            '4e33e19f83a09997914e5650c5c19aa11ce2c79ad00b41c0759770cdfafd9a80')

prepare() {
  patch -Np1 -i "${srcdir}"/fix-tests.patch -d "${srcdir}/${_pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/runtime/Python3"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/runtime/Python3"
  python -m test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/runtime/Python3"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}"/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
